varnishtest "Rewrite URL and Host Header Fields"

server s1 {
        rxreq
        expect req.http.x-host == "sport.example.com"
        expect req.http.x-url == "/index.html"
        expect req.http.host == "example.com"
        expect req.url == "/sport/index.html"
        txresp
} -start

varnish v1 -vcl+backend {
   sub vcl_recv {
       set req.http.x-host = req.http.host;
       set req.http.x-url = req.url;
       
       /* Alternative 1 */
       set req.http.host = regsub(req.http.host, "^sport\.", "");
       set req.url = "/sport" + req.url;

       /* Alternative 2 */
       #if (req.http.host == "sport.example.com") {
       #    set req.http.host = "example.com";
       #    set req.url = "/sport" + req.url;
       #}

       /* Alternative 3 */
       #if (req.http.host ~ "^sport\.") {
       #    set req.http.host = regsub(req.http.host,"^sport\.", "");
       #    set req.url = regsub(req.url, "^", "/sport");
       #}
   }
} -start

client c1 {
        txreq -url "/index.html" -hdr "Host: sport.example.com"
        rxresp
} -run
